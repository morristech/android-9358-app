repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getDefault())
}

retrolambda {
    //jdk System.getenv("JAVA8_HOME")
    //oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
    //jvmArgs '-arg1', '-arg2'
    //defaultMethods false
    //incremental true
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    useLibrary 'org.apache.http.legacy'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        applicationId "com.xmd.manager"
        minSdkVersion 15
        targetSdkVersion 25
        //TODO: release version :
        // 2.5.0.11 << 2.4.2.6 << 2.4.1 << 2.4.0 << 2.3.0, update this comment when you change the versionCode and versionName
        // 2.6.0 ,15 Mainly add the Ranking Activity, which is to load the URL
        // 2.6.3 ,16 Add the Paid Order Consume
        // 3.0.0.17 New designed app, inclulding homepage, chatting, customer-management.
        // 3.0.1.18 data statistics
        // 3.1.0.20 view clubs
        // 3.2.2.24 club authority and customer management
        // TODO: In Development Version
        // 3.0.1.19: Developer Version
        // 3.2.1.23: Add authority
        // 3.5.0 : journal
        // 3.5.1 : journal2 + verification
        // 3.6.6 : tenant service
        // 3.6.8 : bug fix
        // 3.7.0 : verification record
        // 3.7.2 : mainPage data show,Tech Pk,verification by telephone,evaluation label
        versionCode 44
        versionName "3.7.2"

        multiDexEnabled true

        manifestPlaceholders = [
                PACKAGE_NAME      : applicationId,
                //UMENG_APPKEY for release
                UMENG_APPKEY_VALUE: "565d57ade0f55a1504001a62",
                CHANNEL_VALUE     : "xiaomodo",
        ]
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("tools/xmd_app_key.keystore")
            storePassword sign_password
            keyAlias key_alias
            keyPassword sign_password
        }
    }

    buildTypes {

        debug {
            buildConfigField "String", "DEFAULT_UPDATE_SERVER", "\"http://service.xiaomodo.com\""
            manifestPlaceholders = [
                    EASEMOB_APPKEY_VALUE: "xiaomodo#spatest",
                    GETUI_APP_ID        : "8UMRKwWVdoAGROcu6riYn7",
                    GETUI_APP_KEY       : "YMMAertL7F8HXT4aYHTWU3",
                    GETUI_APP_SECRET    : "GQryaLLQfA8SmOoT7kHW15",
                    GETUI_MASTER_SECRET : "O76Q0ZRN2W5OGLhPuTtqW4",
            ]
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "QCLOUD_BUCKET", "\"xmddev\""
            buildConfigField "String", "DEFAULT_UPDATE_SERVER", "\"http://service.xiaomodo.com\""
            manifestPlaceholders = [
                    EASEMOB_APPKEY_VALUE: "xiaomodo#spatest",
                    GETUI_APP_ID        : "8UMRKwWVdoAGROcu6riYn7",
                    GETUI_APP_KEY       : "YMMAertL7F8HXT4aYHTWU3",
                    GETUI_APP_SECRET    : "GQryaLLQfA8SmOoT7kHW15",
                    GETUI_MASTER_SECRET : "O76Q0ZRN2W5OGLhPuTtqW4",
            ]
        }

        prd {
            buildConfigField "String", "QCLOUD_BUCKET", "\"xiaomodo\""
            buildConfigField "String", "DEFAULT_UPDATE_SERVER", "\"http://service.xiaomodo.com\""
            manifestPlaceholders = [
                    EASEMOB_APPKEY_VALUE: "xiaomodo#spa",
                    GETUI_APP_ID        : "3UyoVOksg68ufrcj4KbeA1",
                    GETUI_APP_KEY       : "OEEHRzKVaU6lIPwwcj9eh6",
                    GETUI_APP_SECRET    : "yPtc70a1nmAQsVHqSNISe9",
                    GETUI_MASTER_SECRET : "grpcLvsYZJAOyhkIXJnqK",
            ]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith("release.apk")) {
                // output
                def flavorName = variant.productFlavors[0].name + "_";
                if (flavorName == "prd_") {
                    flavorName = "";
                }
                def fileName = "9358mgr_v${defaultConfig.versionName}.${defaultConfig.versionCode}_${flavorName}xiaomodo_${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    tasks.whenTaskAdded { task ->
        if (task.name.contains('AndroidTest') || task.name.contains('UnitTest')) {
            task.enabled = false
        }
    }

    dataBinding {
        enabled true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':xmd-app')
    compile(name: 'shidou-updater-1.7', ext: 'aar')
    compile(name: 'xmd-image-crop-1.0', ext: 'aar')
    compile 'org.greenrobot:eventbus:3.0.0'
    compile(name: 'xmd-image-selector-1.0', ext: 'aar')
    compile(name: 'xmd-image-tool-1.0', ext: 'aar')
    compile(name: 'permission-tool-1.0', ext: 'aar')
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:support-v4:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibraryVersion}"
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.igexin:sdk:2.6.1.0'
    compile 'com.igexin:getuiext:2.0.3'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    compile project(':loading-view')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.ms-square:expandableTextView:0.1.4'
    compile project(':commonlibrary')
}